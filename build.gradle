// Multi Module Project
buildscript {
	ext {
		springBootVersion = '3.4.5'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.5'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id 'jacoco-report-aggregation' // Jacoco 집계 플러그인
}

repositories {
	mavenCentral()
}

jacoco {
	toolVersion = '0.8.13'
}

// jacoco 집계 대상 모듈 지정
dependencies {
	jacocoAggregation project(':api-module')
}

subprojects { // 모든 하위 모듈들에 이 설정을 적용합니다.
	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'jacoco'

	group 'hongik.triple'
	version '0.0.1-SNAPSHOT'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
	}

	dependencies { // 모든 하위 모듈에 추가 될 의존성 목록입니다.
		// Basic Dependency
		implementation 'org.springframework.boot:spring-boot-starter'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'

		// Lombok Dependency
		implementation 'org.projectlombok:lombok'
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
	}

	test {
		useJUnitPlatform()
		if(project.plugins.hasPlugin('jacoco')) {
			finalizedBy 'jacocoTestReport' // test가 끝나면 jacocoTestReport 동작
		}
	}

	jacoco {
		toolVersion = '0.8.13'
	}

	jacocoTestReport {
		reports {
			xml.required.set(false)
			csv.required.set(false)
			html.required.set(true)
		}
	}
}

tasks.register("jacocoRootReport", JacocoReport) {
	dependsOn = subprojects.collect { it.tasks.named("test") }

	reports {
		xml.required.set(true)
		html.required.set(true)
		csv.required.set(false)
	}

	executionData.setFrom(files(subprojects.collect {
		file("${it.buildDir}/jacoco/test.exec")
	}))

	sourceDirectories.setFrom(files(subprojects.collect {
		file("${it.projectDir}/src/main/java")
	}))

	classDirectories.setFrom(files(subprojects.collect {
		fileTree("${it.buildDir}/classes/java/main") {
			include '**/application/**'
		}
	}))
}